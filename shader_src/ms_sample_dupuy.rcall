#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_GOOGLE_include_directive : enable

#include "../shader_include/definitions.glsl"
#include "../shader_include/util.glsl"
#include "../shader_include/microfacet.glsl"

layout(shaderRecordEXT, std430) buffer sbtData {
    float ior;
    float roughness;
    // bool conductor;
};

layout(location = 0) callableDataInEXT brdfEvaluation evaluation;


vec3 randomWalk(vec3 w, inout float weight, float alpha, float alpha_sq, float ior_rel) {
    float depth = 0.0;
    bool outside = true;
    
    for (int i = 0; i < 32; i++) {

        float extinction = lambdaGgx(w.z, alpha_sq) * w.z;
        float dist = sampleExponential(extinction, evaluation.rand_state);

        depth += dist * w.z;

        if (depth > 0.0) {
            if (!outside) {
                w.z = -w.z;
            }

            return w;
        }

        bool transmitted;

        w = sampleDielectricPhaseFunction(
            -w,
            weight,
            transmitted,
            alpha,
            outside ? ior_rel : 1.0 / ior_rel,
            evaluation.rand_state
        );

        if(transmitted) {
            w.z = -w.z;
            float height = -invHeightCdfUniform(exp(depth));
            depth = log(heightCdfUniform(height));
            outside = !outside;
        }

        // w = sampleConductorPhaseFunction(
        //     -w,
        //     weight,
        //     alpha,
        //     outside ? ior_rel : 1.0 / ior_rel,
        //     evaluation.rand_state
        // );
    }

    return vec3(0.0);
}

void main() {


    float alpha = roughness;    
    float alpha_sq = alpha * alpha;

    float weight = 1.0;

    vec3 w = -evaluation.wo;
    float ior_rel = 1.0 / ior;
    
    bool flip = w.z > 0.0;

    if (flip) { // flip the configuration so we always hit from the top
        w.z = -w.z;
        ior_rel = 1.0 / ior_rel;
    }
    
    w = randomWalk(w, weight, alpha, alpha_sq, ior_rel);

    if (w == vec3(0.0)) {
        evaluation.wi = vec3(0.0);
        evaluation.weight = vec3(0.0);
        return;
    }

    if (flip) {
        w.z = -w.z;
    }

    evaluation.wi = w;
    evaluation.weight = vec3(weight);
}