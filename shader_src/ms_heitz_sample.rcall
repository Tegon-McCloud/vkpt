#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_GOOGLE_include_directive : enable

#include "../shader_include/definitions.glsl"
#include "../shader_include/util.glsl"
#include "../shader_include/microfacet.glsl"

layout(shaderRecordEXT, std430) buffer sbtData {
    float ior;
    float roughness;
};

layout(location = 0) callableDataInEXT brdfEvaluation evaluation;

vec3 sampleConductorPhaseFunction2(
    vec3 wi,
    bool outside_i, // is wi on the outside?
    out bool outside_o, // is wo on the outside?
    float alpha, 
    float alpha_sq,
    float ior_rel, // one over the ior that wi is in 
    inout uint rand_state
) {
    vec3 wm;

    if (outside_i) {
        wm = sampleVndfGgx(wi, alpha, rand_state);
    } else {
        wm = -sampleVndfGgx(-wi, alpha, rand_state);
    }

    float cos_theta_im = dot(wi, wm);

    vec3 wo = 2.0 * cos_theta_im * wm - wi;
    outside_o = outside_i;

    return wo;
}

vec3 sampleDielectricPhaseFunction2(
    vec3 wi,
    bool outside_i, // is wi on the outside?
    out bool outside_o, // is wo on the outside?
    float alpha, 
    float alpha_sq,
    float ior_rel, // one over the ior that wi is in 
    inout uint rand_state
) {
    vec3 wm;

    if (outside_i) {
        wm = sampleVndfGgx(wi, alpha, rand_state);
    } else {
        wm = -sampleVndfGgx(-wi, alpha, rand_state);
    }

    float cos_theta_im = dot(wi, wm);

    if (cos_theta_im < 0.0) {
        wm = -wm;
        cos_theta_im = -cos_theta_im;
    }


    float reflectance = 1.0;

    float sin_theta_tm_sq = ior_rel * ior_rel * (1.0 - cos_theta_im * cos_theta_im);
    float cos_theta_tm;

    if (sin_theta_tm_sq < 1.0) {
        cos_theta_tm = sqrt(1.0 - sin_theta_tm_sq);
        reflectance = reflectanceFresnel(cos_theta_im, cos_theta_tm, ior_rel, 1.0);
    }

    float u = rnd(rand_state);
    vec3 wo;

    if(u < reflectance) {
        wo = 2.0 * cos_theta_im * wm - wi;
        outside_o = outside_i;
    } else {
        wo = (ior_rel * cos_theta_im - cos_theta_tm) * wm - ior_rel * wi;
        outside_o = !outside_i;
    }

    return wo;
}

vec3 randomWalk(vec3 w) {

    bool flip = false;

    if (w.z > 0.0) {
        w = -w;
        flip = true;
    }

    float ior_rel = flip ? 1.0 / ior : ior;

    float alpha = roughness;
    float alpha_sq = alpha * alpha;

    bool outside = true;

    float height = 1.0 + invHeightCdfUniform(0.999);
    
    for (int i = 0; i < 32; i++) {

        bool escaped;

        if(outside) {
            height = sampleHeightGgxUniform(w, height, escaped, alpha_sq, evaluation.rand_state);
        } else {
            height = -sampleHeightGgxUniform(-w, -height, escaped, alpha_sq, evaluation.rand_state);
        }

        if (escaped) {

            if(flip) {
                return -w;
            }

            return w;
        }

        w = sampleDielectricPhaseFunction2(
            -w,
            outside,
            outside,
            alpha,
            alpha_sq,
            outside ? 1.0 / ior_rel : ior_rel,
            evaluation.rand_state
        );
    }

    return vec3(0.0);
}

void main() {
    vec3 w = -evaluation.wo;
    evaluation.wi = randomWalk(w);
    evaluation.weight = vec3(1.0);
}
